import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.report.ReportMergeTask
import nmcp.NmcpAggregationExtension
import nmcp.NmcpExtension
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import java.util.concurrent.TimeUnit

plugins {
    base
    `maven-publish`
    signing
    alias(bt4k.plugins.kotlin.jvm)

    alias(bt4k.plugins.kotlin.allopen) apply false
    alias(libs.plugins.kotlinx.atomicfu)
    alias(libs.plugins.kotlinx.benchmark) apply false
    alias(bt4k.plugins.kover)

    alias(libs.plugins.detekt)
    alias(bt4k.plugins.dependency.management)

    alias(bt4k.plugins.dokka)
    alias(libs.plugins.test.logger)

    alias(bt4k.plugins.nmcp.aggregation)
    alias(bt4k.plugins.nmcp) apply false
}

val rootLibs = libs
val bt4kCatalog = extensions.getByType<org.gradle.api.artifacts.VersionCatalogsExtension>().named("bt4k")
fun bt4kLibrary(alias: String) = bt4kCatalog.findLibrary(alias).get()
fun bt4kVersion(alias: String): String {
    val version = bt4kCatalog.findVersion(alias).get()
    return version.requiredVersion
        .ifBlank { version.preferredVersion }
        .ifBlank { version.strictVersion }
}


val centralPublishing = resolveCentralPublishingConfig()
val centralUser: String = centralPublishing.username
val centralPassword: String = centralPublishing.password
val centralSnapshotsParallelism: Int = providers
    .gradleProperty("centralSnapshotsParallelism")
    .map(String::toInt)
    .orElse(4)
    .get()

val projectGroup = providers.gradleProperty("projectGroup").get()
val baseVersion = providers.gradleProperty("baseVersion").get()
val snapshotVersion = providers.gradleProperty("snapshotVersion").get()

fun Project.isNonPublishedModule(): Boolean =
    path == ":examples" || path.startsWith(":examples:")

fun Project.isRunnableExampleModule(): Boolean =
    path.startsWith(":examples:")

allprojects {
    group = projectGroup
    version = baseVersion + snapshotVersion

    repositories {
        mavenCentral()
        maven {
            name = "central-snapshots"
            url = uri("https://central.sonatype.com/repository/maven-snapshots/")
        }
    }
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor(1, TimeUnit.DAYS)
    }
}

subprojects {
    if (isNonPublishedModule()) {
        return@subprojects
    }

    apply(plugin = "com.gradleup.nmcp")

    configurations.matching { it.name.startsWith("nmcp") }.configureEach {
        resolutionStrategy.eachDependency {
            if (requested.group == "org.jetbrains.kotlinx" && requested.name.startsWith("kotlinx-serialization")) {
                useVersion("1.9.0")
                because("nmcp runtime compatibility")
            }
        }
    }

    plugins.withId("com.gradleup.nmcp") {
        extensions.configure<NmcpExtension>("nmcp") {
            publishAllPublicationsToCentralPortal {
                username.set(centralUser)
                password.set(centralPassword)
                publishingType.set("AUTOMATIC")
                uploadSnapshotsParallelism.set(centralSnapshotsParallelism)
            }
        }
    }
}

subprojects {
    // 플랫폼 BOM 모듈은 java-platform 플러그인을 사용하므로 Java/Kotlin 설정을 건너뜁니다.
    if (name == "bluetape4k-text-bom") return@subprojects

    val isNonPublished = isNonPublishedModule()

    apply {
        plugin<JavaLibraryPlugin>()
        plugin("org.jetbrains.kotlin.jvm")
        plugin("com.adarshr.test-logger")
        if (isRunnableExampleModule()) {
            plugin("application")
        }
        if (!isNonPublished) {
            plugin("org.jetbrains.kotlinx.atomicfu")
            plugin("org.jetbrains.kotlinx.kover")
            plugin("maven-publish")
            plugin("signing")
            plugin("io.spring.dependency-management")
            plugin("org.jetbrains.dokka")
        }
    }

    pluginManager.withPlugin("org.jetbrains.kotlin.jvm") {
        kotlin {
            jvmToolchain(21)
            compilerOptions {
                languageVersion.set(KotlinVersion.KOTLIN_2_3)
                apiVersion.set(KotlinVersion.KOTLIN_2_3)
                freeCompilerArgs = listOf(
                    "-Xjsr305=strict",
                    "-jvm-default=enable",
                    "-Xstring-concat=indy",
                    "-Xcontext-parameters",
                    "-Xannotation-default-target=param-property"
                )
                val experimentalAnnotations = listOf(
                    "kotlin.RequiresOptIn",
                    "kotlin.ExperimentalStdlibApi",
                    "kotlin.contracts.ExperimentalContracts",
                    "kotlin.experimental.ExperimentalTypeInference",
                    "kotlinx.coroutines.ExperimentalCoroutinesApi",
                    "kotlinx.coroutines.InternalCoroutinesApi",
                    "kotlinx.coroutines.FlowPreview",
                    "kotlinx.coroutines.DelicateCoroutinesApi",
                )
                freeCompilerArgs.addAll(experimentalAnnotations.map { "-opt-in=$it" })
            }
        }
    }

    if (!isNonPublished) {
        pluginManager.withPlugin("org.jetbrains.kotlinx.atomicfu") {
            atomicfu {
                transformJvm = true
                jvmVariant = "VH"
            }
        }
    }

    tasks {
        abstract class TestMutexService: BuildService<BuildServiceParameters.None>
        abstract class SigningMutexService: BuildService<BuildServiceParameters.None>

        val testMutex = gradle.sharedServices.registerIfAbsent("test-mutex", TestMutexService::class) {
            maxParallelUsages.set(1)
        }
        val signingMutex = gradle.sharedServices.registerIfAbsent("signing-mutex", SigningMutexService::class) {
            maxParallelUsages.set(1)
        }

        compileJava { options.isIncremental = true }
        compileKotlin { compilerOptions { incremental = true } }

        test {
            usesService(testMutex)
            useJUnitPlatform()
            jvmArgs(
                "-Xshare:off",
                "-Xms2M",
                "-Xmx4G",
                "-XX:+UseG1GC",
                "-XX:+UnlockExperimentalVMOptions",
                "-XX:+EnableDynamicAgentLoading",
                "--enable-preview",
                "-Didea.io.use.nio2=true"
            )
            testLogging {
                showExceptions = true
                showCauses = true
                showStackTraces = true
                events("failed")
            }
        }

        if (!isNonPublished) {
            withType<Sign>().configureEach {
                usesService(signingMutex)
            }
        }

        testlogger {
            theme = com.adarshr.gradle.testlogger.theme.ThemeType.MOCHA_PARALLEL
            showFullStackTraces = true
        }

        val reportMerge = register<ReportMergeTask>("reportMerge") {
            output.set(rootProject.layout.buildDirectory.file("reports/detekt/merged.xml"))
        }
        withType<Detekt>().configureEach detekt@{
            finalizedBy(reportMerge)
            reportMerge.configure { input.from(this@detekt.xmlReportFile) }
        }

        jar {
            manifest.attributes["Specification-Title"] = project.name
            manifest.attributes["Specification-Version"] = project.version
            manifest.attributes["Implementation-Title"] = project.name
            manifest.attributes["Implementation-Version"] = project.version
            manifest.attributes["Automatic-Module-Name"] = project.name.replace('-', '.')
            manifest.attributes["Created-By"] =
                "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
        }

        if (!isNonPublished) {
            dokka {
                dokkaPublications.html {
                    outputDirectory.set(layout.buildDirectory.asFile.get().resolve("javadoc"))
                }
                dokkaSourceSets.configureEach {
                    includes.from(project.files("README.md"))
                }
            }
        }

        clean {
            doLast {
                delete("./.project")
                delete("./out")
                delete("./bin")
            }
        }

        // atomicfu 변환 산출물 생성 뒤 kover coverage 수집이 실행되도록 순서를 명시합니다.
        if (!isNonPublished) {
            matching { it.name == "koverGenerateArtifactJvm" }.configureEach {
                mustRunAfter(matching { it.name == "transformMainAtomicfu" })
            }
        }
    }

    if (!isNonPublished) {
        dependencyManagement {
            setApplyMavenExclusions(false)
            imports {
                mavenBom(bt4kLibrary("bluetape4k-bom").get().toString())
                mavenBom("org.jetbrains.kotlinx:kotlinx-coroutines-bom:${bt4kVersion("kotlinx-coroutines")}")
                mavenBom("org.jetbrains.kotlin:kotlin-bom:${bt4kVersion("kotlin")}")
                mavenBom(rootLibs.junit.bom.get().toString())
                mavenBom("org.testcontainers:testcontainers-bom:${bt4kVersion("testcontainers")}")
            }

            dependencies {

                // <central-catalog-local-aliases>

                dependency("com.fasterxml.jackson:jackson-bom:${bt4kVersion("jackson")}")

                dependency("org.awaitility:awaitility-kotlin:${bt4kVersion("awaitility")}")

                dependency("org.jetbrains.kotlin:kotlin-bom:${bt4kVersion("kotlin")}")

                dependency("org.jetbrains.kotlinx:kotlinx-coroutines-bom:${bt4kVersion("kotlinx-coroutines")}")

                dependency("org.slf4j:jcl-over-slf4j:${bt4kVersion("slf4j")}")

                dependency("org.slf4j:jul-to-slf4j:${bt4kVersion("slf4j")}")

                dependency("org.slf4j:log4j-over-slf4j:${bt4kVersion("slf4j")}")

                dependency("org.testcontainers:testcontainers-bom:${bt4kVersion("testcontainers")}")

                dependency("org.testcontainers:testcontainers-junit-jupiter:${bt4kVersion("testcontainers")}")

                // </central-catalog-local-aliases>
                dependency("org.slf4j:slf4j-api:${bt4kVersion("slf4j")}")
            }
        }
    }

    dependencies {
        if (isNonPublished) {
            add("implementation", platform("org.jetbrains.kotlin:kotlin-bom:${bt4kVersion("kotlin")}"))
            add(
                "implementation",
                platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:${bt4kVersion("kotlinx-coroutines")}"),
            )
            add("testImplementation", platform(rootLibs.junit.bom))
        }

        add("api", rootLibs.jetbrains.annotations)

        add("implementation", rootLibs.kotlin.stdlib)
        add("implementation", rootLibs.kotlin.reflect)
        add("testImplementation", rootLibs.kotlin.test)
        add("testImplementation", rootLibs.kotlin.test.junit5)

        add("implementation", rootLibs.kotlinx.coroutines.core)
        if (!isNonPublished) {
            add("implementation", rootLibs.kotlinx.atomicfu)
        }

        if (isNonPublished) {
            add("api", "org.slf4j:slf4j-api:${bt4kVersion("slf4j")}")
        } else {
            add("api", bt4kLibrary("slf4j-api"))
        }
        add("testImplementation", rootLibs.logback)
        add("testImplementation", bt4kLibrary("jcl-over-slf4j"))
        add("testImplementation", bt4kLibrary("jul-to-slf4j"))
        add("testImplementation", bt4kLibrary("log4j-over-slf4j"))

        add("testImplementation", rootLibs.junit.jupiter)
        add("testRuntimeOnly", rootLibs.junit.platform.engine)

        add("testImplementation", rootLibs.awaitility.kotlin)
        add("testImplementation", rootLibs.mockk)
    }

    if (!isNonPublished) {
        publishing {
            publications {
                create<MavenPublication>("BluetapeText") {
                    val sourcesJar = tasks.register<Jar>("sourcesJar") {
                        archiveClassifier.set("sources")
                        from(sourceSets["main"].allSource)
                    }
                    val javadocJar = tasks.register<Jar>("javadocJar") {
                        archiveClassifier.set("javadoc")
                        from(layout.buildDirectory.asFile.get().resolve("javadoc"))
                    }
                    from(components["java"])
                    artifact(sourcesJar)
                    artifact(javadocJar)

                    pom {
                        name.set(project.name)
                        description.set("Kotlin/JVM text processing library — tokenizers, language detection, text search — part of the bluetape4k ecosystem")
                        url.set("https://github.com/bluetape4k/bluetape4k-text")
                        licenses {
                            license {
                                name.set("The Apache License, Version 2.0")
                                url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
                            }
                        }
                        developers {
                            developer {
                                id.set("debop")
                                name.set("Sunghyouk Bae")
                                email.set("sunghyouk.bae@gmail.com")
                            }
                        }
                        scm {
                            connection.set("scm:git:git://github.com/bluetape4k/bluetape4k-text.git")
                            developerConnection.set("scm:git:ssh://github.com/bluetape4k/bluetape4k-text.git")
                            url.set("https://github.com/bluetape4k/bluetape4k-text")
                        }
                    }
                }
            }
            repositories {
                mavenCentral()
                maven {
                    name = "central-snapshots"
                    url = uri("https://central.sonatype.com/repository/maven-snapshots/")
                }
            }
        }

        configurePublishingSigning("BluetapeText")
    }
}

extensions.configure<NmcpAggregationExtension>("nmcpAggregation") {
    centralPortal {
        username.set(centralUser)
        password.set(centralPassword)
        publishingType.set("AUTOMATIC")
        uploadSnapshotsParallelism.set(centralSnapshotsParallelism)
    }
}

dependencies {
    subprojects
        .filterNot { it.isNonPublishedModule() }
        .forEach { add("nmcpAggregation", project(it.path)) }
}

dependencies {
    subprojects
        .filter { it.name != "bluetape4k-text-bom" && !it.isNonPublishedModule() }
        .forEach { sub ->
            kover(dependencies.project(mapOf("path" to sub.path)))
        }
}

// atomicfu 변환 산출물 생성 뒤 kover coverage 수집이 실행되도록 순서를 명시합니다.
tasks.matching { it.name == "koverGenerateArtifactJvm" }.configureEach {
    mustRunAfter(tasks.matching { it.name == "transformMainAtomicfu" })
}
